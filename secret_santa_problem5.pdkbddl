;; CISC 813 - Assignment 8 (Week 10: Epismteic Planning Model)
;; secret santa domain for a multi-agent epistemic setting
;; key features
;; - multiple agents moving between rooms
;; - secert santa assignments encoded as draws, drawer, target
;; - gifts are corresponded to recipients with gift-for, gift, target
;; - RPMEP belief modalities [a] and <a> for knowlege and possibility
;; - actions for drawing names from the bow , picking up gifts from the gift room, communicating assignment,
;; and for one problem inferring assignment based on other facts
;; - a variety of problems will be presented to exhibit in lecture concepts through the model

(define (domain secret-santa)

    (:agents a b c)

    (:types loc gift)

    (:predicates
        ;; world-level secret santa info
        (draws ?drawer - agent ?target - agent)
        (assigned ?drawer - agent)

        ;; gifts and locations
        (has-gift ?drawer - agent ?g - gift)
        (gift-at ?g - gift ?l - loc)
        {AK}(gift-for ?g - gift ?target - agent)

        ;; rooms and connectivity
        {AK}(at ?ag - agent ?l - loc)
        {AK}(connected ?l1 ?l2 - loc)

        ;; no self-draws
        {AK}(distinct ?x - agent ?y - agent)

        ;; C's candidate targets for B, or possible targets
        (b-candidate-a)
        (b-candidate-b)
        (b-candidate-c)

        ;; marker that C has performed the final inference
        (inferred-b-drew-c)
    )

    ;; public movement between rooms
    (:action walk
        :derive-condition always
        :parameters (?ag - agent ?from ?to - loc)
        :precondition (and (at ?ag ?from)
                           (connected ?from ?to))
        :effect (and (at ?ag ?to)
                     (!at ?ag ?from))
    )

    ;; drawing a name in the bowl-room
    ;; Anyone in bowl-room can observe it 
    (:action draw-name
        :derive-condition (at $agent$ bowl-room)
        :parameters (?drawer - agent ?target - agent)
        :precondition (and (at ?drawer bowl-room)
                           (!assigned ?drawer)
                           (distinct ?drawer ?target))
        :effect (and (draws ?drawer ?target)
                     (assigned ?drawer))
    )

    ;; picking the correct gift in the gifts-room
    (:action pick-gift
        :derive-condition (at $agent$ gifts-room)
        :parameters (?drawer - agent ?g - gift ?target - agent)
        :precondition (and (at ?drawer gifts-room)
                           (gift-at ?g gifts-room)
                           (gift-for ?g ?target)
                           (draws ?drawer ?target))
        :effect (and (has-gift ?drawer ?g)
                     (!gift-at ?g gifts-room))
    )

    ;; reasoning action 1:
    ;; C rules out "B drew A" after C knows C drew A.
    ;; (if C has target A, no one else can have A.)
    (:action rule-out-b-a
        :derive-condition never
        :parameters ()
        :precondition (and
                        [c](draws c a)
                        (b-candidate-a))
        :effect (and
                    (!b-candidate-a))
    )

    ;; reasoning action 2:
    ;; C rules out "B drew B" after C knows A drew B.
    ;; (If A has target B, no one else can have B.)
    (:action rule-out-b-b
        :derive-condition never
        :parameters ()
        :precondition (and
                        [c](draws a b)
                        (b-candidate-b))
        :effect (and
                    (!b-candidate-b))
    )

    ;; final inference action: C infers that B drew C.
    ;; this upgrades the last remaining candidate to belief
    (:action infer-b-drew-c
        :derive-condition never
        :parameters ()
        :precondition (and
                        [c](draws a b)
                        [c](draws c a)
                        (!b-candidate-a)
                        (!b-candidate-b)
                        (b-candidate-c))
        :effect (and
                    [c](draws b c)
                    (inferred-b-drew-c))
    )
)

;; problem 5: structural deduction using inference actions 
;; - inference of agent b's assignment without direct communication of it
;; - needed to adjust domain to be different from previous domain, including extra fluents, reasoning actions,
;; one final inference action, and the goal requires the inference to ensure that reasoning is used instead of only messaging
;; demonstrates syntactic inference, conditional effects for reasoning steps, and KD45 closure ensuring introspective beliefs 

(define (problem santa-deduction-5)

    (:domain secret-santa)

    (:objects
        main-room bowl-room gifts-room - loc
        g_ab g_bc g_ca - gift
    )

    (:projection )
    (:task valid_generation)
    (:init-type complete)

    (:init

        ;; triangular room connectivity
        (connected main-room bowl-room)
        (connected bowl-room main-room)

        (connected main-room gifts-room)
        (connected gifts-room main-room)

        (connected bowl-room gifts-room)
        (connected gifts-room bowl-room)

        ;; all agents start in the main-room
        (forall ?ag - agent (at ?ag main-room))

        ;; no one has drawn yet
        (!assigned a)
        (!assigned b)
        (!assigned c)

        ;; gifts start in the gifts-room
        (gift-at g_ab gifts-room)
        (gift-at g_bc gifts-room)
        (gift-at g_ca gifts-room)

        ;; gift mapping
        (gift-for g_ab b)
        (gift-for g_bc c)
        (gift-for g_ca a)

        ;; distinctness 
        (distinct a b)
        (distinct a c)
        (distinct b a)
        (distinct b c)
        (distinct c a)
        (distinct c b)

        ;; C's initial candidates for B's target:
        ;; Initially, B could have A, B, or C.
        (b-candidate-a)
        (b-candidate-b)
        (b-candidate-c)

        ;; inference has not yet been performed
        (!inferred-b-drew-c)
    )

    ;; no nested beliefs here, so depth 1 is enough.
    (:depth 1)

    (:goal
        (and
            ;; world assignment
            (draws a b)
            (draws b c)
            (draws c a)

            ;; gifts collected correctly
            (has-gift a g_ab)
            (has-gift b g_bc)
            (has-gift c g_ca)

            ;; each agent knows their own draw
            [a](draws a b)
            [b](draws b c)
            [c](draws c a)

            ;; C also knows A's draw (from observing A's draw)
            [c](draws a b)

            ;; deductive knowledge
            [c](draws b c)

            ;; and we require that the explicit infer action was used
            (inferred-b-drew-c)

            ;; everyone ends in the main-room
            (forall ?ag - agent (at ?ag main-room))
        )
    )
)
