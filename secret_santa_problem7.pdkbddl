;; CISC 813 - Assignment 8 (Week 10: Epismteic Planning Model)
;; secret santa domain for a multi-agent epistemic setting
;; key features
;; - multiple agents moving between rooms
;; - secert santa assignments encoded as draws, drawer, target
;; - gifts are corresponded to recipients with gift-for, gift, target
;; - RPMEP belief modalities [a] and <a> for knowlege and possibility
;; - actions for drawing names from the bow , picking up gifts from the gift room, communicating assignment,
;; and for one problem inferring assignment based on other facts
;; - a variety of problems will be presented to exhibit in lecture concepts through the model

(define (domain secret-santa)

    (:agents a b c d e)

    (:types loc gift)

    (:predicates
        ;; secret santa assignment
        (draws ?drawer - agent ?target - agent)
        (assigned ?drawer - agent)

        ;; gifts and locations
        (has-gift ?drawer - agent ?g - gift)
        (gift-at ?g - gift ?l - loc)
        {AK}(gift-for ?g - gift ?target - agent)

        ;; rooms and connectivity
        {AK}(at ?ag - agent ?l - loc)
        {AK}(connected ?l1 ?l2 - loc)

        ;; no self-draws
        {AK}(distinct ?x - agent ?y - agent)

        ;; ensure both communication modes are used
        (used-whisper)
        (used-talk)
    )

    ;; public movement between rooms
    (:action walk
        :derive-condition always
        :parameters (?ag - agent ?from ?to - loc)
        :precondition (and (at ?ag ?from)
                           (connected ?from ?to))
        :effect (and (at ?ag ?to)
                     (!at ?ag ?from))
    )

    ;; drawing a name in the bowl-room, anyone in bowl-room can observe it
    (:action draw-name
        :derive-condition (at $agent$ bowl-room)
        :parameters (?drawer - agent ?target - agent)
        :precondition (and (at ?drawer bowl-room)
                           (!assigned ?drawer)
                           (distinct ?drawer ?target))
        :effect (and (draws ?drawer ?target)
                     (assigned ?drawer))
    )

    ;; picking the correct gift in the gifts-room, anyone in gifts-room can see it
    (:action pick-gift
        :derive-condition (at $agent$ gifts-room)
        :parameters (?drawer - agent ?g - gift ?target - agent)
        :precondition (and (at ?drawer gifts-room)
                           (gift-at ?g gifts-room)
                           (gift-for ?g ?target)
                           (draws ?drawer ?target))
        :effect (and (has-gift ?drawer ?g)
                     (!gift-at ?g gifts-room))
    )

    ;; private whisper about who someone drew
    ;; only the receiver updates their belief
    ;; force at least one whisper in the plan
    (:action whisper-assign
        :derive-condition never
        :parameters (?speaker ?receiver ?target - agent ?l - loc)
        :precondition (and (at ?speaker ?l)
                           (at ?receiver ?l)
                           (draws ?speaker ?target))
        :effect (and
                    [?receiver](draws ?speaker ?target)
                    (used-whisper)
                )
    )

    ;; loud talk about who someone drew
    ;; anyone in the same room reasons about this
    ;; force at least one talk in the plan
    (:action talk-assign
        :derive-condition (at $agent$ ?l)
        :parameters (?speaker ?receiver ?target - agent ?l - loc)
        :precondition (and (at ?speaker ?l)
                           (at ?receiver ?l)
                           (draws ?speaker ?target))
        :effect (and
                    [?receiver](draws ?speaker ?target)
                    (used-talk)
                )
    )
)

;; problem 7: 5 agent complex problem with nested beliefs and uncertainty
;; - world is the same with now with 5 agents to ensure that the model holds with increased complexity
;; - one agent eventually finds out the enture assignment
;; - both methods of communication included and some uncertainty
;; - demonstrates multi-agent KD45 reasoning at depth 2, uncertain beliefs via <a>(phi),
;; distributed knowledge through public and private communication, interaction between nested modalities, and observation

(define (problem santa-five-7)

    (:domain secret-santa)

    (:objects
        main-room bowl-room gifts-room - loc
        g_ab g_bc g_cd g_de g_ea - gift
    )

    (:projection )
    (:task valid_generation)
    (:init-type complete)

    (:init

        ;; triangle connectivity among rooms
        (connected main-room bowl-room)
        (connected bowl-room main-room)

        (connected main-room gifts-room)
        (connected gifts-room main-room)

        (connected bowl-room gifts-room)
        (connected gifts-room bowl-room)

        ;; all agents start in the main-room
        (forall ?ag - agent (at ?ag main-room))

        ;; no one has drawn yet
        (!assigned a)
        (!assigned b)
        (!assigned c)
        (!assigned d)
        (!assigned e)

        ;; gifts start in the gifts-room
        (gift-at g_ab gifts-room)
        (gift-at g_bc gifts-room)
        (gift-at g_cd gifts-room)
        (gift-at g_de gifts-room)
        (gift-at g_ea gifts-room)

        ;; gift mapping: cycle a->b, b->c, c->d, d->e, e->a
        (gift-for g_ab b)
        (gift-for g_bc c)
        (gift-for g_cd d)
        (gift-for g_de e)
        (gift-for g_ea a)

        ;; distinctness 
        (distinct a b) (distinct a c) (distinct a d) (distinct a e)
        (distinct b a) (distinct b c) (distinct b d) (distinct b e)
        (distinct c a) (distinct c b) (distinct c d) (distinct c e)
        (distinct d a) (distinct d b) (distinct d c) (distinct d e)
        (distinct e a) (distinct e b) (distinct e c) (distinct e d)

        ;; flags are false
        (!used-whisper)
        (!used-talk)
    )

    ;; need depth 2 for nested beliefs [a][c](...) and [d][c](...)
    (:depth 2)

    (:goal
        (and
            ;; world assignment (cycle)
            (draws a b)
            (draws b c)
            (draws c d)
            (draws d e)
            (draws e a)

            ;; gifts collected correctly
            (has-gift a g_ab)
            (has-gift b g_bc)
            (has-gift c g_cd)
            (has-gift d g_de)
            (has-gift e g_ea)

            ;; each agent knows their own draw
            [a](draws a b)
            [b](draws b c)
            [c](draws c d)
            [d](draws d e)
            [e](draws e a)

            ;; C knows the full assignment
            [c](draws a b)
            [c](draws b c)
            [c](draws c d)
            [c](draws d e)
            [c](draws e a)

            ;; nested beliefs:
            ;; A knows that C knows B's draw
            [a][c](draws b c)

            ;; D knows that C knows E's draw
            [d][c](draws e a)

            ;; E remains uncertain about A's draw:
            ;; E considers it possible that A drew B, and possible that A did not.
            <e>(draws a b)
            <e>(!draws a b)

            ;; ensure at least one whisper and one talk were used
            (used-whisper)
            (used-talk)

            ;; everyone ends up back in the main-room
            (forall ?ag - agent (at ?ag main-room))
        )
    )
)
