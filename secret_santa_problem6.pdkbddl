;; CISC 813 - Assignment 8 (Week 10: Epismteic Planning Model)
;; secret santa domain for a multi-agent epistemic setting
;; key features
;; - multiple agents moving between rooms
;; - secert santa assignments encoded as draws, drawer, target
;; - gifts are corresponded to recipients with gift-for, gift, target
;; - RPMEP belief modalities [a] and <a> for knowlege and possibility
;; - actions for drawing names from the bow , picking up gifts from the gift room, communicating assignment,
;; and for one problem inferring assignment based on other facts
;; - a variety of problems will be presented to exhibit in lecture concepts through the model

(define (domain secret-santa)

    (:agents a b c)

    (:types loc gift)

    (:predicates
        ;; secret santa assignment
        (draws ?drawer - agent ?target - agent)
        (assigned ?drawer - agent)

        ;; gifts and locations
        (has-gift ?drawer - agent ?g - gift)
        (gift-at ?g - gift ?l - loc)
        {AK}(gift-for ?g - gift ?target - agent)

        ;; rooms and connectivity
        {AK}(at ?ag - agent ?l - loc)
        {AK}(connected ?l1 ?l2 - loc)

        ;; no self-draws
        {AK}(distinct ?x - agent ?y - agent)

        ;; ensure both modalities are used
        (used-whisper)
        (used-talk)
    )

    ;; public movement between rooms
    (:action walk
        :derive-condition always
        :parameters (?ag - agent ?from ?to - loc)
        :precondition (and (at ?ag ?from)
                           (connected ?from ?to))
        :effect (and (at ?ag ?to)
                     (!at ?ag ?from))
    )

    ;; drawing a name in the bowl-room, anyone in bowl-room can observe it
    (:action draw-name
        :derive-condition (at $agent$ bowl-room)
        :parameters (?drawer - agent ?target - agent)
        :precondition (and (at ?drawer bowl-room)
                           (!assigned ?drawer)
                           (distinct ?drawer ?target))
        :effect (and (draws ?drawer ?target)
                     (assigned ?drawer))
    )

    ;; picking the correct gift in the gifts-room, anyone in gifts-room can see it
    (:action pick-gift
        :derive-condition (at $agent$ gifts-room)
        :parameters (?drawer - agent ?g - gift ?target - agent)
        :precondition (and (at ?drawer gifts-room)
                           (gift-at ?g gifts-room)
                           (gift-for ?g ?target)
                           (draws ?drawer ?target))
        :effect (and (has-gift ?drawer ?g)
                     (!gift-at ?g gifts-room))
    )

    ;; private whisper about who someone drew
    ;; only the receiver updates their belief
    ;; and sets used-whisper to force at least one whisper in the plan
    (:action whisper-assign
        :derive-condition never
        :parameters (?speaker ?receiver ?target - agent ?l - loc)
        :precondition (and (at ?speaker ?l)
                           (at ?receiver ?l)
                           (draws ?speaker ?target))
        :effect (and
                    [?receiver](draws ?speaker ?target)
                    (used-whisper)
                )
    )

    ;;loud talk about who someone drew
    ;; anyone in the same room (via derive-condition) reasons about this
    ;; also sets used-talk to force at least one talk in the plan
    (:action talk-assign
        :derive-condition (at $agent$ ?l)
        :parameters (?speaker ?receiver ?target - agent ?l - loc)
        :precondition (and (at ?speaker ?l)
                           (at ?receiver ?l)
                           (draws ?speaker ?target))
        :effect (and
                    [?receiver](draws ?speaker ?target)
                    (used-talk)
                )
    )
)

;; problem 6: nested beliefs and mixed communication
;; - same three agent set up as all the rest but not using both whisper and loud talk
;; - now a dpeth of two to accomodate 
;; - one agent gradually learns the full assignment over time
;; - demonstrates epistemic nesting with bounded depth of 2, combining private and public updates in a single plane, 
;; KD45 introspection at depth 2, conditioned mutual awareness, and perspective sensitive updates 

(define (problem santa-mixed-6)

    (:domain secret-santa)

    (:objects
        main-room bowl-room gifts-room - loc
        g_ab g_bc g_ca - gift
    )

    (:projection )
    (:task valid_generation)
    (:init-type complete)

    (:init

        ;; triangle connectivity among rooms
        (connected main-room bowl-room)
        (connected bowl-room main-room)

        (connected main-room gifts-room)
        (connected gifts-room main-room)

        (connected bowl-room gifts-room)
        (connected gifts-room bowl-room)

        ;; all agents start in the main-room
        (forall ?ag - agent (at ?ag main-room))

        ;; no one has drawn yet
        (!assigned a)
        (!assigned b)
        (!assigned c)

        ;; gifts start in the gifts-room
        (gift-at g_ab gifts-room)
        (gift-at g_bc gifts-room)
        (gift-at g_ca gifts-room)

        ;; gift mapping: each gift is for one target
        (gift-for g_ab b)
        (gift-for g_bc c)
        (gift-for g_ca a)

        ;; distinctness 
        (distinct a b)
        (distinct a c)
        (distinct b a)
        (distinct b c)
        (distinct c a)
        (distinct c b)

        ;; false at first
        (!used-whisper)
        (!used-talk)
    )

    ;; need depth 2 for nested beliefs [a][c](...)
    (:depth 2)

    (:goal
        (and
            ;; world assignment 
            (draws a b)
            (draws b c)
            (draws c a)

            ;; gifts collected correctly
            (has-gift a g_ab)
            (has-gift b g_bc)
            (has-gift c g_ca)

            ;; each agent knows their own draw
            [a](draws a b)
            [b](draws b c)
            [c](draws c a)

            ;; C knows B drew C 
            [c](draws b c)

            ;; C knows A drew B
            [c](draws a b)

            ;; nested belief: A knows that C knows A drew B
            [a][c](draws a b)

            ;; ensure at least one whisper and one talk were used
            (used-whisper)
            (used-talk)

            ;; everyone ends up back in the main-room
            (forall ?ag - agent (at ?ag main-room))
        )
    )
)
