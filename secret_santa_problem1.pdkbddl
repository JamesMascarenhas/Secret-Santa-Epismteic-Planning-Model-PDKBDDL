;; CISC 813 - Assignment 8 (Week 10: Epismteic Planning Model)
;; secret santa domain for a multi-agent epistemic setting
;; key features
;; - multiple agents moving between rooms
;; - secert santa assignments encoded as draws, drawer, target
;; - gifts are corresponded to recipients with gift-for, gift, target
;; - RPMEP belief modalities [a] and <a> for knowlege and possibility
;; - actions for drawing names from the bow , picking up gifts from the gift room, communicating assignment,
;; and for one problem inferring assignment based on other facts
;; - a variety of problems will be presented to exhibit in lecture concepts through the model

(define (domain secret-santa)

    (:agents a b c)

    (:types loc gift)

    (:predicates
        ;; secret santa assignment
        (draws ?drawer - agent ?target - agent)

        ;; each agent may draw at most once
        (assigned ?drawer - agent)

        ;; gift g is currently held by drawer
        (has-gift ?drawer - agent ?g - gift)

        ;; gift g is located in room l 
        (gift-at ?g - gift ?l - loc)

        ;; static mapping from gift to intended target
        {AK}(gift-for ?g - gift ?target - agent)

        ;; everyone always knows where everyone is
        {AK}(at ?ag - agent ?l - loc)

        ;; everyone always knows the room connectivity
        {AK}(connected ?l1 ?l2 - loc)

        ;; static distinctness between agents (for no self-draws)
        {AK}(distinct ?x - agent ?y - agent)
    )

    ;; public movement between rooms (everyone sees movement)
    (:action walk
        :derive-condition   always
        :parameters         (?ag - agent ?from ?to - loc)
        :precondition       (and (at ?ag ?from)
                                 (connected ?from ?to))
        :effect             (and (at ?ag ?to)
                                 (!at ?ag ?from))
    )

    ;; draw a name from the bowl in the bowl-room
    ;; anyone in the bowl-room observes who drew whom
    ;; one draw per drawer, and no self-draw
    (:action draw-name
        :derive-condition (at $agent$ bowl-room)
        :parameters       (?drawer - agent ?target - agent)
        :precondition     (and (at ?drawer bowl-room)
                               (!assigned ?drawer)
                               (distinct ?drawer ?target))
        :effect           (and (draws ?drawer ?target)
                               (assigned ?drawer))
    )

    ;; pick the correct gift in the gifts-room for the drawn target
    ;; anyone in the gifts-room sees who picks which gift
    (:action pick-gift
        :derive-condition (at $agent$ gifts-room)
        :parameters       (?drawer - agent ?g - gift ?target - agent)
        :precondition     (and (at ?drawer gifts-room)
                               (gift-at ?g gifts-room)
                               (gift-for ?g ?target)
                               (draws ?drawer ?target))
        :effect           (and (has-gift ?drawer ?g)
                               (!gift-at ?g gifts-room))
    )
)

;; problem 1: baseline secert santa, three agents and no communication
;; - all agents move through the rooms each of which draw a name from the bowl room
;; then at some point travel to the gift room and select the matching gift
;; - each agent knows only their own assignment
;; - all gifts are collected corectly and everyone returns to the main room 
;; - movement and gift picking with static map knowledge
;; - demonstrates KD45 belief structure at depth 1, basic observation modeled with derive-conditions,
;; syntactic RP-MEP representation of beliefs as fluents, proper doxastic knowledge base (PDKB) initalization,
;; and nmo disjunction and no nested modal formulas

(define (problem santa-draws-1)

    (:domain secret-santa)

    (:objects
        main-room bowl-room gifts-room - loc
        g_ab g_bc g_ca - gift
    )

    (:projection )
    (:task valid_generation)

    (:init-type complete)

    (:init

        ;; triangle connectivity: each room connected to the other two
        (connected main-room bowl-room)
        (connected bowl-room main-room)

        (connected main-room gifts-room)
        (connected gifts-room main-room)

        (connected bowl-room gifts-room)
        (connected gifts-room bowl-room)

        ;; all agents start in the main room
        (forall ?ag - agent (at ?ag main-room))

        ;; no one has drawn yet
        (!assigned a)
        (!assigned b)
        (!assigned c)

        ;; gifts start on the table in the gifts-room
        (gift-at g_ab gifts-room)
        (gift-at g_bc gifts-room)
        (gift-at g_ca gifts-room)

        ;; static mapping: which gift is for whom
        (gift-for g_ab b)   ; g_ab is the gift for b
        (gift-for g_bc c)   ; g_bc is the gift for c
        (gift-for g_ca a)   ; g_ca is the gift for a

        ;; static distinctness facts 
        (distinct a b)
        (distinct a c)
        (distinct b a)
        (distinct b c)
        (distinct c a)
        (distinct c b)
    )

    ;; only need depth 1, no nested beliefs yet
    (:depth 1)

    (:goal
        (and
            ;; concrete Secret Santa assignment for this instance:
            ;;  a draws b, b draws c, c draws a
            (draws a b)
            (draws b c)
            (draws c a)

            ;; each drawer has picked the correct gift
            (has-gift a g_ab)
            (has-gift b g_bc)
            (has-gift c g_ca)

            ;; each agent knows their own draw
            [a](draws a b)
            [b](draws b c)
            [c](draws c a)

            ;; everyone ends up back in the main-room
            (forall ?ag - agent (at ?ag main-room))
        )
    )
)
